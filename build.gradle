plugins {
    id 'java'
    id 'groovy'
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id 'org.sonatype.gradle.plugins.scan' version '3.0.0'
}

group 'io.github.kamilszewc'
version '5.14'
java {
    withJavadocJar()
    withSourcesJar()
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

dependencies {
    // Apache commons
    implementation 'org.apache.commons:commons-lang3:3.17.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.36'
    annotationProcessor 'org.projectlombok:lombok:1.18.36'

    // Testing
    testImplementation 'org.apache.groovy:groovy-all:4.0.26'
    testImplementation 'org.spockframework:spock-core:2.4-M5-groovy-4.0'
}

test {
    useJUnitPlatform()
}


artifacts {
    archives javadocJar, sourcesJar
}

publishing {
    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                if (project.properties.containsKey('mavenCentralTokenUser')) {
                    username = "$mavenCentralTokenUser"
                }
                if (project.properties.containsKey('mavenCentralTokenPassword')) {
                    password = "$mavenCentralTokenPassword"
                }
            }
        }
    }
    publications {

        mavenJava(MavenPublication) {
            artifactId = 'resourcewatcher'
            from components.java
            pom {
                name = 'Resource Watcher'
                description = 'Java-based Linux, Mac, Windows resources watcher (cpu, gpu, ram, disk, ...)'
                url = 'https://github.com/kamilszewc/resourcewatcher'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'kszewc'
                        name = "Kamil Szewc"
                        email = 'kamil.szewc@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/kamilszewc/resourcewatcher.git'
                    developerConnection = 'scm:git:https://github.com/kamilszewc/resourcewatcher.git'
                    url = 'https://github.com/kamilszewc/resourcewatcher.git'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
    sign configurations.archives
}

ossIndexAudit {
    allConfigurations = false
    useCache = false
    outputFormat = 'DEFAULT'
}